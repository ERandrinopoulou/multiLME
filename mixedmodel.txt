model { 
    for (i in 1:n1) {
        for (j in offset1[i]:(offset1[i+1] - 1)) {
            muy1[j] <- inprod(c_betas1[1:ncx1], Xc1[j, 1:ncx1]) +
                 inprod(b1[i, 1:ncz1], Z1[j, 1:ncz1]) + alpha12 * muy2[j]
            y1[j] ~ dnorm(muy1[j], tau1)
        }
    }
    for (i in 1:n2) {
        for (j in offset2[i]:(offset2[i+1] - 1)) {
            muy2[j] <- inprod(c_betas2[1:ncx2], Xc2[j, 1:ncx2]) +
                 inprod(b2[i, 1:ncz2], Z2[j, 1:ncz2])
            y2[j] ~ dnorm(muy2[j], tau2)
        }
    }
    for (i1 in 1:n1) {
        b1[i1, 1:ncz1] ~ dmnorm(mu01[], inv.D1[, ])
    } 
    for (i2 in 1:n2) {
        b2[i2, 1:ncz2] ~ dmnorm(mu02[], inv.D2[, ])
    } 
    for (k1 in 1:ncx1) {
        c_betas1[k1] ~ dnorm(0.0, tau_betas1)
    } 
    for (k2 in 1:ncx2) {
        c_betas2[k2] ~ dnorm(0.0, tau_betas2)
    } 
    alpha12 ~ dnorm(0.0, tau_alpha)
    tau1 ~ dgamma(priorA.tau1, priorB.tau1)
    tau2 ~ dgamma(priorA.tau2, priorB.tau2)
    sigma1 <- 1/sqrt(tau1)
    sigma2 <- 1/sqrt(tau2)
    for (l1 in 1:ncz1) {
        inv.D1[l1, l1] ~ dgamma(0.01, 0.01) 
    } 
    for (l2 in 1:ncz2) {
        inv.D2[l2, l2] ~ dgamma(0.01, 0.01) 
    } 
    betas1[1] = c_betas1[1] - inprod(c_betas1[2:ncx1], means_X1) 
    betas1[2:ncx1] = c_betas1[2:ncx1] 
    betas2[1] = c_betas2[1] - inprod(c_betas2[2:ncx2], means_X2) 
    betas2[2:ncx2] = c_betas2[2:ncx2] 
}

